plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    defaultConfig {
        applicationId 'com.eightbit.samsprung'
        compileSdk 34
        minSdk 30
        targetSdk 34
        versionName "2.5.9"
        versionCode = versionName.replace(".","").toInteger()

        vectorDrawables {
            useSupportLibrary = true
        }
        splits {
            abi {
                enable true
                reset()
                //noinspection ChromeOsAbiSupport
                include 'arm64-v8a'
                universalApk false
            }
        }
        renderscriptTargetApi 31
        renderscriptSupportModeEnabled true

        def commit = 'git rev-parse --short HEAD'.execute().text.trim()
        buildConfigField "String", "COMMIT", "\""+commit+"\""
        project.archivesBaseName = "${rootProject.name}-${commit}"
    }

    namespace defaultConfig.applicationId

    flavorDimensions += "source"
    productFlavors {
        github {
            dimension "source"
            manifestPlaceholders.putAll([
                    installPermission: "android.permission.REQUEST_INSTALL_PACKAGES",
                    accessibilityPermission: "android.permission.BIND_ACCESSIBILITY_SERVICE",
                    accessibilityFilter: "android.accessibilityservice.AccessibilityService",
                    accessibilityMetaName: "android.accessibilityservice",
                    accessibilityMetaRes: "@xml/accessibility_service"
            ])
            buildConfigField "boolean", "GOOGLE_PLAY", "false"
        }
        google {
            dimension "source"
            isDefault true
            manifestPlaceholders.putAll([
                    installPermission: "${defaultConfig.applicationId}.UPDATING",
                    accessibilityPermission: "${defaultConfig.applicationId}.SERVICES",
                    accessibilityFilter: "${defaultConfig.applicationId}.SERVICES",
                    accessibilityMetaName: "${defaultConfig.applicationId}.SERVICES",
                    accessibilityMetaRes: "@xml/placeholder_service"
            ])
            buildConfigField "boolean", "GOOGLE_PLAY", "true"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            initWith buildTypes.release
            debuggable true
        }
    }

    try {
        def keystoreProps = new Properties()
        keystoreProps.load(new FileInputStream(rootProject.file("keystore.properties")))
        signingConfigs {
            document {
                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storeFile file(keystoreProps['storeFile'])
                storePassword keystoreProps['storePassword']
            }
        }
        buildTypes.release.signingConfig signingConfigs.document
        buildTypes.debug.signingConfig signingConfigs.document
    } catch (FileNotFoundException ignored) { }

    def javaVersion = JavaVersion.VERSION_11
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    lint {
        abortOnError false
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "${project.archivesBaseName}-${output.baseName}.apk"
        }
    }
}

dependencies {
    implementation 'androidx.activity:activity-ktx:1.8.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.window:window-java:1.2.0'

    implementation 'com.android.billingclient:billing:6.1.0'

    implementation 'com.github.8bitdream:AndroidFastScroll:4cb96477d5'

    implementation 'com.google.android.material:material:1.10.0'
    implementation 'com.google.android.play:core:1.10.3'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'
}
